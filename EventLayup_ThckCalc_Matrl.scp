//HeaderImportExport-->
//SiteId=0
//Enterprise=True
//ScriptType=1
//ScriptGroup=Stackup
//HeaderImportExport<--
// Rev: 1.7.0.04 - JDB - Include Drill Loss in calculation

{ This script is fired as a user opens the dropdown box in the
  Layup module for selecting a material at a certain position
  in the Layup.

  Script has to calculate/adjust the parameters for an entry in
  the materials table when it is used at this position in the Layup.
  Ea, if a Gap/Combo is seletced, then the thickness has to be
  re-calculated depending on the layup above/below.

  Second option is to exclude Gap/Combo's from the list of available
  options at this position. Ea certain prepregs or combo's with that
  prepregtype on the outside are not suitable for using at this
  position in the layup, the resin filling can be to low.

  Fnc.SysLayupParamTable has the layup where the material is used
  Fnc.SysMaterialsTable has all combinations then can be selected.

  Fields to be set/changed in the Fnc.SysMaterialsTable:

  =====> Layup materials like Core, Prepreg, Gap/Combo, RCC, etc, etc

  'ThicknessCalc' (float)
         thickness as the material is used at this position in the Layup

  'DKCalc_Top', 'DKCalc_Bottom (float)
         the DK for the top/bottom part of the materials as it is pressed,
         ea prepreg it will be the DK of the Resin, for Cores it will be zero

  'DKMiddle' (float)
         The DK of the middle section of the material. ea for Prepreg this
         will be the calculated DK based on the DK of the Resing and Glass
         using the weight of the glass and the final top-to-top thickness of
         the layers that the prepreg is pressed. For Cores and other materials
         that do not change thickness at pressing, this is the 'standard' DK for
         the materials

  'Exclude' (boolean)
         False: valid option at this position, True: item is excluded
         from the list of valid options at this position.
         (see also 'DispColor')

  'DispColor' (integer)
         Option to specify the color for row in the popup grid. Can be used ea
         to show Combo's/Gap that have poor Resin filling in Yellow or Red
         Possible values for this field: clRed, clYellow, clBlue, clFusia, clAqua
         (see also 'Exclude')

  === => SolderMask materials

  'Exclude' (boolean)
         Always False, unless you want to exclude a SolderMask (type) from the
         drop-down for selecting materials
         (see also 'DispColor')

  'DispColor' (integer)
         Option to specify the color for row in the popup grid. Possible values
         for this field: clRed, clYellow, clBlue, .... (see also 'Exclude')

  'DKCalc_Middle'
         DK of the Soldermask

}

Const
  ShowDebugMsg = True;

procedure SetSpecificDkForFrequency;
var
  FreqId: integer;
  FreqStr: string;
begin
  FreqId := Fnc.SysImpedanceFrequencyId;
  if FreqId < 1 then
    FreqId := Fnc.SysDefaultImpedanceFrequencyId;

  if FreqId > 0 then
  begin
    FreqStr := IntToStr(FreqId);

    if not (Fnc.SysMaterialsTable.Fields.FindField('DKOriginal_' + FreqStr) = nil) then
    begin
      Fnc.SysMaterialsTable.First;

      while not Fnc.SysMaterialsTable.Eof do
      begin
        Fnc.SysMaterialsTable.Edit;
        Fnc.SysMaterialsTable.FieldByName('DKOriginal').AsFloat := Fnc.SysMaterialsTable.FieldByName('DKOriginal_' + FreqStr).AsFloat;
        Fnc.SysMaterialsTable.FieldByName('DKAverage').AsFloat := Fnc.SysMaterialsTable.FieldByName('DKAverage_' + FreqStr).AsFloat;
        Fnc.SysMaterialsTable.FieldByName('DKGlass').AsFloat := Fnc.SysMaterialsTable.FieldByName('DKGlass_' + FreqStr).AsFloat;
        Fnc.SysMaterialsTable.FieldByName('DKResin').AsFloat := Fnc.SysMaterialsTable.FieldByName('DKResin_' + FreqStr).AsFloat;
        Fnc.SysMaterialsTable.FieldByName('DF').AsFloat := Fnc.SysMaterialsTable.FieldByName('DF_' + FreqStr).AsFloat;
        Fnc.SysMaterialsTable.Post;

        Fnc.SysMaterialsTable.Next;
      end;
    end;
  end;
end;

procedure GetLayerParam(bAbove: boolean; var LayerHeight: float; var CopperDensity: float);
var
    CuLayerType : String;
begin
  LayerHeight := 0.0;
  CopperDensity := 100;


  with Fnc.SysLayupParamTable do
  begin
    // find the item we should calculate
    if Locate('ActiveRecord', True, SetOf([])) then
    begin

      // re-position to the item above/below
      if bAbove then
        Prior
      else
        Next;

      // check if there is an item above/below
      if not (Bof or Eof) then
      begin

        // If this is a layer, then use the height/thickness of that layer
        if FieldByName('ItemType').AsString = 'LYR' then
        begin
          // Get the height/thickness of the Layer
          LayerHeight := FieldByName('GapIns').AsFloat;
          CopperDensity := FieldByName('LayupCuDensity').AsFloat;
          CuLayerType   := UpperCase(FieldByName('Type').AsString);

          // if copper density is same as that of predefined layer types but does not match layer type
          // then user changed layer type manually so update copper density based on new layer type.
          // If copper density is 0.0 then update copper density based on layer type.
          // Any other copper density is manually specified density. DO NOT edit density.

          if ((CopperDensity = 40) and (CuLayerType <> 'SIGNAL')) or
             ((CopperDensity = 65.25) and (CuLayerType <> 'MIXED')) or
             ((CopperDensity = 90.00) and (CuLayerType <> 'POWER_GROUND'))or
              (CopperDensity = 0.0) then

            case UpperCase(FieldByName('Type').AsString) of
              'SIGNAL':       CopperDensity := 40;
              'MIXED':        CopperDensity := 65.25;
              'POWER_GROUND': CopperDensity := 90.00;
            end;

          // Update Cu Density field in the stack-up dialog.
          Edit;
          FieldByName('LayupCuDensity').AsFloat := CopperDensity;
          Post;
        end;
      end;

    end;
  end;
end;

// CalculateDrillLoss calculates the total height that is lost due to holes.
// The volume lost is back calculated to figure out the total height that is lost due to holes.
function CalculateDrillLoss(OriginalHeight : double) : double;
var
  VolumeLost : double;
  Area : double;
  ExistingVolume : double;
  NewVolume : double;
  HoleHeight : double;
  HoleRadius : double;
  NumberOfHoles : double;
begin
  // In this example, we will calculate the loss by panel.  This means that the number
  //  of holes per panel must be filled in for the drill tool.  You could alternatively
  //  calculate the loss by part if you want.
  Fnc.SendDebugString('CalculateDrillLoss(' + FloatToStr(OriginalHeight) + ')');

  result := OriginalHeight;
  VolumeLost := 0.0;
  Fnc.SysDrillToolTable.First;

  while not Fnc.SysDrillToolTable.EOF do
  begin
    if (Fnc.SysDrillToolTable.FieldByName('AffectedTop').AsBoolean) then
    begin
      // figure out what the drill hole height is...
      HoleHeight := Fnc.SysDrillToolTable.FieldByName('HalveHoleDepthTop').AsFloat; // (must divide by 2 in some cases as prepreg may
                                                                                    // come from both top & bottom - this is already done)
      HoleRadius := (Fnc.SysDrillToolTable.FieldByName('FINISH_SIZE').AsFloat / 2.0); // (finish size is a diameter)
      NumberOfHoles := Fnc.SysDrillToolTable.FieldByName('TOTNUMHOLES').AsFloat;  // calculating by panel
      //NumberOfHoles := Fnc.SysDrillToolTable.FieldByName('NUMHOLES').AsFloat;  // calculating by part

      // result := Pi * R (radius) * R (radius) * HoleHeight  * Number of holes
      VolumeLost := VolumeLost + (3.14159265 * HoleRadius * HoleRadius * (HoleHeight) * NumberOfHoles);
        Fnc.SendDebugString('Hole Radius: ' + FloatToStr(HoleRadius));
        Fnc.SendDebugString('Hole Height: ' + FloatToStr(HoleHeight));
        Fnc.SendDebugString('NumHoles: ' + FloatToStr(NumberOfHoles));
    end;

    if (Fnc.SysDrillToolTable.FieldByName('AffectedBottom').AsBoolean) then
    begin
      // figure out what the drill hole height is...
      HoleHeight := Fnc.SysDrillToolTable.FieldByName('HalveHoleDepthBottom').AsFloat;
      HoleRadius := (Fnc.SysDrillToolTable.FieldByName('FINISH_SIZE').AsFloat / 2.0);
      NumberOfHoles := Fnc.SysDrillToolTable.FieldByName('TOTNUMHOLES').AsFloat;  // calculating by panel
      //NumberOfHoles := Fnc.SysDrillToolTable.FieldByName('NUMHOLES').AsFloat;  // calculating by part

      // result := Pi * R (radius) * R (radius) * HoleHeight (must divide by 2 as prepreg will come from both top & bottom) * Number of holes
      VolumeLost := VolumeLost + (3.14159265 * HoleRadius * HoleRadius * (HoleHeight) * NumberOfHoles);
      Fnc.SendDebugString('Hole Radius: ' + FloatToStr(HoleRadius));
      Fnc.SendDebugString('Hole Height: ' + FloatToStr(HoleHeight));
      Fnc.SendDebugString('NumHoles: ' + FloatToStr(NumberOfHoles));
    end;

    Fnc.SysDrillToolTable.Next;
  end;

  // we can calculate the area pretty easily...

  // calculation by panel
  Area := Fnc.SysRequirementsDetails.FieldByName('PANELWIDTH').AsFloat * Fnc.SysRequirementsDetails.FieldByName('PANELLENGTH').AsFloat;
  // calculation by part
  //Area := Fnc.SysRequirementsDetails.FieldByName('PARTWIDTH').AsFloat * Fnc.SysRequirementsDetails.FieldByName('PARTLENGTH').AsFloat;

  ExistingVolume := Area * OriginalHeight;
  NewVolume := ExistingVolume - VolumeLost;
  if NewVolume < 0.0 then
    NewVolume := 0.0;

  Fnc.SendDebugString('CalculateDrillLoss OriginalVolume: ' + FloatToStr(ExistingVolume));
  Fnc.SendDebugString('CalculateDrillLoss VolumeLost: ' + FloatToStr(VolumeLost));
  Fnc.SendDebugString('CalculateDrillLoss NewVolume: ' + FloatToStr(NewVolume));


  // now back calculate to get our height
  if Area > 0.0 then
    result := NewVolume / Area;

  Fnc.SendDebugString('CalculateDrillLoss NewHeight: ' + FloatToStr(result));
  //ShowMessage('Original Height: ' + FloatToStr(OriginalHeight) + '  New Height: ' + FloatToStr(result));
end;

function CalculateThePressedThickness(var PressedThickness: double; HeightUp: float; HeightDn: float; DensityUp: float; DensityDn: float): boolean;
begin
 //Fnc.SendDebugString('Pressed Thk1: ' + FloatToStr(PressedThickness));
  PressedThickness := Fnc.SysMaterialsTable.FieldByName('ThicknessAfterPressing').AsFloat;
  Fnc.SendDebugString('Pressed Thk2: ' + FloatToStr(PressedThickness));

  { =======================================================================
    Now the following calculation is customer specific. The following
    calculation is just an example for demo purposes. Depending on the
    parameters of the Up/Down layertype, the material will flow more/less
    into the imaged layer
    =======================================================================}

  // do a correction because of the image density, density of 100 means
  // there is no image, ea pressing foil on prepreg
  if DensityUp < 100 then
    PressedThickness := PressedThickness - (((100 - DensityUp) / 100) * HeightUp);

  if DensityDn < 100 then
    PressedThickness := PressedThickness - (((100 - DensityDn) / 100) * HeightDn);

  PressedThickness := CalculateDrillLoss(PressedThickness);

end;

function CalculateDK(TotalThickness: double): double;
var
  GlassThickness: double;
  PartDKGlass: double;
  PartDKResin: double;
begin
  GlassThickness := 0;
  PartDKGlass := 0;
  PartDKResin := 0;

  Result := Fnc.SysMaterialsTable.FieldByName('DKOriginal').AsFloat;

  if Result = 0 then
  begin

    // Er(prepreg) = (Er(glass) * GT / TT) +(Er(resin) * (TT - GT) / TT)
    // Where:
    // Er(glass) = 6.33
    // GT = the total glass thickness of the plies of prepreg
    //      (The glass thickness for each style of prepreg can Be found from the GlassInfo page
    //      of the Toronto.xls file by multiplying the glass wt column by 0.000525530.)
    // TT = total thickness of the prepreg (as it is pressed)

    GlassThickness := Fnc.SysMaterialsTable.FieldByName('GlassWeight').AsFloat * 0.000525530;

    try
      PartDKGlass := Fnc.SysMaterialsTable.FieldByName('DKGlass').AsFloat * GlassThickness / TotalThickness;
      PartDKResin := Fnc.SysMaterialsTable.FieldByName('DKResin').AsFloat * (TotalThickness - GlassThickness) / TotalThickness;
      Result := PartDKGlass + PartDKResin;

      if ShowDebugMsg then
      begin
        Fnc.SendDebugString('');
        Fnc.SendDebugStrings(Fnc.SysMaterialsTable.FieldByName('Description').AsString, Fnc.SysMaterialsTable.FieldByName('Construction').AsString);
        Fnc.SendDebugString('');

        Fnc.SendDebugStrings('GlassThickness: ' + Fnc.SysMaterialsTable.FieldByName('GlassWeight').AsString + ' * 0.000525530'
                          , FloatToStr(GlassThickness));

        Fnc.SendDebugStrings('TotalThickness: ', FloatToStr(TotalThickness));

        Fnc.SendDebugStrings('PartDKGlass: ' +  Fnc.SysMaterialsTable.FieldByName('DKGlass').AsString
                                          + ' * ' + FloatToStr(GlassThickness)
                                          + ' / ' + FloatToStr(TotalThickness)
                          , FloatToStr(PartDKGlass));

        Fnc.SendDebugStrings('PartDKResin: ' + Fnc.SysMaterialsTable.FieldByName('DKResin').AsString
                                          + ' * (' + FloatToStr(TotalThickness) + ' - ' + FloatToStr(GlassThickness) + ')'
                                          + ' / ' + FloatToStr(TotalThickness)
                          , FloatToStr(PartDKResin));

        Fnc.SendDebugStrings('DK: ' + FloatToStr(PartDKGlass) + ' + ' + FloatToStr(PartDKResin)
                          , FloatToStr(Result));

        Fnc.SendDebugString('');
      end;

    except
      Result := 0;
    end;
  end;
end;

function GetDKResin: double;
begin
  if Fnc.SysMaterialsTable.FieldByName('DKResin').AsFloat = 0 then
    Result := Fnc.SysMaterialsTable.FieldByName('DKOriginal').AsFloat
  else
    Result := Fnc.SysMaterialsTable.FieldByName('DKResin').AsFloat;

end;

// Calculate for all the materials what this means if they are used under these conditions
procedure Process_Laminate;
var
  HeightUp: double;
  HeightMiddle: double;
  HeightDn: double;
  CopperDensityUp: double;
  CopperDensityDn: double;
  DKUp: double;
  DKMiddle: double;
  DKDn: double;
  InvalidOption: boolean;
  TempValue : double;
begin
//  Fnc.CS.SendMsg('==> Process_Laminate');

  DKUp := 0;
  DKMiddle := 0;
  DKDn := 0;
  InvalidOption := False;

  GetLayerParam(True, HeightUp, CopperDensityUp);
  GetLayerParam(False, HeightDn, CopperDensityDn);

  if not Fnc.SysMaterialsTable.FieldByName('AdjustThickness').AsBoolean then
  begin
    // as per materials definition, Height/Thickness does not change at pressing, ea Core
    HeightMiddle := Fnc.SysMaterialsTable.FieldByName('ThicknessAfterPressing').AsFloat;
    DKMiddle := CalculateDK(HeightMiddle);
  end
  else
  begin
    // as per material definition, the final thickness changes depending on the parameters of the layer Up/Down
    InvalidOption := CalculateThePressedThickness(HeightMiddle, HeightUp, HeightDn, CopperDensityUp, CopperDensityDn);

    // DK of top section might change (like with Prepreg)
    if HeightUp > 0 then
      DKUp := GetDKResin;

    DKMiddle := CalculateDK(HeightMiddle);

    // DK of bottom section might change (like with Prepreg)
    if HeightDn > 0 then
      DKDn := GetDKResin;
  end;

  Fnc.SysMaterialsTable.Edit;

  Fnc.SysMaterialsTable.FieldByName('ThicknessCalc').AsFloat := HeightMiddle;
  Fnc.SysMaterialsTable.FieldByName('DKCalc_Top').AsFloat := DKUp;
  Fnc.SysMaterialsTable.FieldByName('DKCalc_Middle').AsFloat := DKMiddle;
  Fnc.SysMaterialsTable.FieldByName('DKCalc_Bottom').AsFloat := DKDn;
  //Fnc.SysMaterialsTable.FieldByName('Exclude').AsBoolean := InvalidOption;

  // Init these two values
  Fnc.SysMaterialsTable.FieldByName('Exclude').AsBoolean := False;
  Fnc.SysMaterialsTable.FieldByName('DispColor').AsInteger := 0;

{ Enable these lines to exclude Gap/Combo's that run under the minimum
  thickness as specified in the Materials Table

  if Fnc.SysMaterialsTable.FieldByName('MinThickness').AsFloat > 0 then
  begin
    if DKMiddle < Fnc.SysMaterialsTable.FieldByName('MinThickness').AsFloat then
      Fnc.SysMaterialsTable.FieldByName('Exclude').AsBoolean := True;
  end;
}

  // show options that are not suitable in Red
  if (HeightMiddle < 0) or (Fnc.SysMaterialsTable.FieldByName('MinThickness').AsFloat > 0) then
  begin
    TempValue := HeightMiddle - Fnc.SysMaterialsTable.FieldByName('MinThickness').AsFloat;

    if TempValue < 0.001 then
      // Warning
      Fnc.SysMaterialsTable.FieldByName('DispColor').AsInteger := clYellow;

    if TempValue < 0.0 then
      // Material does not fit at this position
      Fnc.SysMaterialsTable.FieldByName('DispColor').AsInteger := clRed;
  end;

  Fnc.SysMaterialsTable.Post;
end;

procedure Process_SolderMask;
begin
  with Fnc.SysMaterialsTable do
  begin
    Edit;
    FieldByName('Exclude').AsBoolean := False;
    FieldByName('DKCalc_Middle').AsFloat := FieldByName('DKOriginal').AsFloat;
    FieldByName('ThicknessCalc').AsFloat := FieldByName('ThicknessAfterPressing').AsFloat;
    Post;
  end;
end;

begin
  //Fnc.OpenDataGrid(Fnc.SysRequirementsDetails, nil,nil,false);
//  Fnc.OpenDataGrid(Fnc.SysDrillToolTable, nil,nil,false);
  // ShowMessage(FloatToStr(CalculateDrillLoss));
//  Fnc.CS.SendMsg('===> EventLayup');

  SetSpecificDkForFrequency;

  Fnc.SysMaterialsTable.First;
  while not Fnc.SysMaterialsTable.Eof do
  begin

//    Fnc.CS.SendMsg('Case');

    case Fnc.SysMaterialsTable.FieldByName('MaterialsClassId').AsInteger of
      1: Process_Laminate;
      2: Process_SolderMask;
    end;

//    Fnc.CS.SendMsg('Next');

    Fnc.SysMaterialsTable.Next;
  end;

//  Fnc.CS.SendMsg('<=== EventLayup');

      Fnc.SysRequirementsDetails.Edit;
      Fnc.SysRequirementsDetails.FieldByName('ScriptVersion').AsString := 'zzzz';
      Fnc.SysRequirementsDetails.Post;

end;

